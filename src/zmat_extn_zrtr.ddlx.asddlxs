@Metadata.layer: #PARTNER
annotate entity ZI_connection_ZRTR with
{
  @UI.facet: [{ id: 'Connection',
                purpose: #STANDARD,
                type   : #IDENTIFICATION_REFERENCE,
                position: 10,
                label: 'Connection Details'
             },
             {  id: 'Flight',
                purpose: #STANDARD,
                type   : #LINEITEM_REFERENCE,
                position: 20,
                label: 'Flight Details',
                targetElement: '_Flight'
             }]

  @UI.lineItem: [{ position: 10, label: 'Airline' }]
  @UI.identification: [{ position: 10, label: 'Airline' }]
  @Search.defaultSearchElement: true
  CarrierId;
  @UI.lineItem: [{ position: 20 }]
  @UI.identification: [{ position: 20 }]
  @Search.defaultSearchElement: true
  ConnectionId;
  @UI.lineItem: [{ position: 30, label: 'Departure Id' }]
  @UI.identification: [{ position: 30, label: 'Departure Id' }]
  @Search.defaultSearchElement: true
  @UI.selectionField: [{ position: 10 }]
  @Consumption.valueHelpDefinition: [{ entity: {
                                           name: 'ZI_AIRPORT_ZRTR',
                                           element: 'AirportId'
                                       } }]
  AirportFromId;
  @UI.lineItem: [{ position: 40 }]
  @UI.identification: [{ position: 40 }]
  @UI.selectionField: [{ position: 20 }]
  @Consumption.valueHelpDefinition: [{ entity: {
                                           name: 'ZI_AIRPORT_ZRTR',
                                           element: 'AirportId'
                                       } }]
  @EndUserText.label: 'Destination Id'
  AirportToId;
  @UI.lineItem: [{ position: 50, label: 'Departure Time' }]
  @UI.identification: [{ position: 50 }]
  DepartureTime;
  @UI.lineItem: [{ position: 60, label: 'Arrival Time' }]
  @UI.identification: [{ position: 60 }]
  ArrivalTime;
  @UI.identification: [{ position: 70 }]
  Distance;
  @UI.identification: [{ position: 80 }]
  DistanceUnit;
}